plugins {
    id 'java'
}

java {
    // Target oldest tested Java runtime
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    // AWS Java libraries https://github.com/aws/aws-lambda-java-libs
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.3'

     // SLF4J logging
    implementation 'org.slf4j:slf4j-nop:2.0.9'
}

task packageFat(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
    dirMode = 0755
    fileMode = 0755
}

task packageLibs(type: Zip) {
    into('java/lib') {
        from configurations.runtimeClasspath
    }
    dirMode = 0755
    fileMode = 0755
}

task packageSkinny(type: Zip) {
    from compileJava
    from processResources
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

build.dependsOn packageSkinny
